module Helpers exposing (onEnter, prepareQuery, resetViewportOf, viewMaybe)

import Browser.Dom as Dom
import Dict exposing (Dict)
import Element exposing (Element, none)
import Html.Events as HE
import Json.Decode as Decode
import Maybe.Extra as ME
import Task
import Url


{-|

    A view helper that will either render the value of
    'body' with a given `viewFunc`, or return `Element.none`
    indicating nothing should be rendered.

    `viewFunc` can be partially applied with a `language` value
    allowing the body to be rendered in response to the user's
    selected language parameter.

-}
viewMaybe : (a -> Element msg) -> Maybe a -> Element msg
viewMaybe viewFunc maybeBody =
    ME.unpack (\() -> none) viewFunc maybeBody


onEnter : msg -> Element.Attribute msg
onEnter msg =
    Element.htmlAttribute
        (HE.on "keyup"
            (Decode.field "key" Decode.string
                |> Decode.andThen
                    (\key ->
                        if key == "Enter" then
                            Decode.succeed msg

                        else
                            Decode.fail "Not the enter key"
                    )
            )
        )


prepareQuery : Maybe String -> Dict String (List String)
prepareQuery maybeQuery =
    case maybeQuery of
        Nothing ->
            Dict.empty

        Just qry ->
            List.foldr addParam Dict.empty (String.split "&" qry)


addParam : String -> Dict String (List String) -> Dict String (List String)
addParam segment dict =
    case String.split "=" segment of
        [ rawKey, rawValue ] ->
            case Url.percentDecode rawKey of
                Nothing ->
                    dict

                Just key ->
                    case Url.percentDecode rawValue of
                        Nothing ->
                            dict

                        Just value ->
                            Dict.update key (addToParametersHelp value) dict

        _ ->
            dict


addToParametersHelp : a -> Maybe (List a) -> Maybe (List a)
addToParametersHelp value maybeList =
    case maybeList of
        Nothing ->
            Just [ value ]

        Just list ->
            Just (value :: list)


resetViewportOf : msg -> String -> Cmd msg
resetViewportOf sendMsg id =
    Dom.setViewportOf id 0 0
        |> Task.attempt
            (\_ -> sendMsg)
